Source -> Declarations .

Declarations -> Declaration .
Declarations -> Declarations Declaration .

Declaration -> TYP IDENTIFIER COLON Type SEMIC .
Declaration -> VAR IDENTIFIER COLON Type SEMIC .
Declaration -> FUN IDENTIFIER LPARENTHESIS ParametersOpt RPARENTHESIS COLON Type FunctionBodyOpt SEMIC .

Statement -> Expression .
Statement -> Expression ASSIGN Expression .
Statement -> IF Expression THEN Statements ElseOpt END .
Statement -> WHILE Expression DO Statements END .

Expression -> ORXORExpression .

ORXORExpression -> ANDExpression .
ORXORExpression -> ORXORExpression IOR ANDExpression .
ORXORExpression -> ORXORExpression XOR ANDExpression .

ANDExpression -> RelationalExpression .
ANDExpression -> ANDExpression AND RelationalExpression .

RelationalExpression -> ADDSUBExpression .
RelationalExpression -> ADDSUBExpression EQU ADDSUBExpression .
RelationalExpression -> ADDSUBExpression NEQ ADDSUBExpression .
RelationalExpression -> ADDSUBExpression GTE ADDSUBExpression .
RelationalExpression -> ADDSUBExpression LTE ADDSUBExpression .
RelationalExpression -> ADDSUBExpression GTH ADDSUBExpression .
RelationalExpression -> ADDSUBExpression LTH ADDSUBExpression .

ADDSUBExpression -> MULDIVExpression .
ADDSUBExpression -> ADDSUBExpression ADD MULDIVExpression .
ADDSUBExpression -> ADDSUBExpression SUB MULDIVExpression .

MULDIVExpression -> PrefixExpression .
MULDIVExpression -> MULDIVExpression MUL PrefixExpression .
MULDIVExpression -> MULDIVExpression DIV PrefixExpression .
MULDIVExpression -> MULDIVExpression MOD PrefixExpression .

PrefixExpression -> PostfixExpression .
PrefixExpression -> ADD PrefixExpression .
PrefixExpression -> SUB PrefixExpression .
PrefixExpression -> NOT PrefixExpression .
PrefixExpression -> NEW LPARENTHESIS Type RPARENTHESIS .
PrefixExpression -> DEL LPARENTHESIS Expression RPARENTHESIS .
PrefixExpression -> DATA PrefixExpression .
PrefixExpression -> ADDR PrefixExpression .

PostfixExpression -> AtomicExpression .
PostfixExpression -> PostfixExpression LBRACKET Expression RBRACKET .
PostfixExpression -> PostfixExpression DOT IDENTIFIER .
PostfixExpression -> LPARENTHESIS Expression COLON Type RPARENTHESIS .	
PostfixExpression -> LPARENTHESIS Expression RPARENTHESIS .

AtomicExpression -> INTCONST | VOIDCONST | CHARCONST | BOOLCONST | PTRCONST | STRCONST | IDENTIFIER ArgumentsOpt .
AtomicExpression -> LBRACE Statements COLON Expression WhereOpt RBRACE .



Type -> INT | VOID | CHAR | BOOL | IDENTIFIER .
Type -> ARR LBRACKET Expression RBRACKET Type .
Type -> PTR Type .
Type -> LPARENTHESIS Type RPARENTHESIS .
Type -> REC LPARENTHESIS Parameters RPARENTHESIS .

FunctionBodyOpt -> .
FunctionBodyOpt -> ASSIGN Expression .

ParametersOpt -> .
ParametersOpt -> Parameters .

Parameters -> IDENTIFIER COLON Type .
Parameters -> Parameters COMMA IDENTIFIER COLON Type .

Statements -> Statement SEMIC .
Statements -> Statements Statement SEMIC .

WhereOpt -> .
WhereOpt -> WHERE Declarations .

ArgumentsOpt -> .
ArgumentsOpt -> LPARENTHESIS RPARENTHESIS .
ArgumentsOpt -> LPARENTHESIS Arguments RPARENTHESIS .

Arguments -> Expression .
Arguments -> Arguments COMMA Expression .

ElseOpt -> .
ElseOpt -> ELSE Statements .